plugins {
    id 'java'
    id 'maven-publish'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

def libraryGroupId = 'com.reynke.sloud'
def libraryArtifactId = 'databaseutilities'
def libraryVersion = '1.0.2'
def libraryMainClassName = 'SloudDatabaseUtilities'

group libraryGroupId + '.' + libraryArtifactId
version libraryVersion

def mainClassName = group + '.' + libraryMainClassName

repositories {
    mavenCentral()
}

configurations {
    // Configuration that holds jars to include in the jar
    extraLibs
}

dependencies {
    implementation group: 'com.google.inject', name: 'guice', version: '5.0.1'
    extraLibs group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
    extraLibs group: 'com.sun.xml.bind', name: 'jaxb-core', version: '3.0.1'
    extraLibs group: 'com.sun.xml.bind', name: 'jaxb-impl', version: '3.0.1'
    extraLibs group: 'mysql', name: 'mysql-connector-java', version: '8.0.24'
    extraLibs group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '2.7.3'
    extraLibs group: 'org.postgresql', name: 'postgresql', version: '42.2.22'
    extraLibs group: 'com.zaxxer', name: 'HikariCP', version: '4.0.3'
    extraLibs group: 'org.hibernate', name: 'hibernate-hikaricp', version: '5.5.0.Final'
    testImplementation group: 'junit', name: 'junit', version: '8.9.0'
    configurations.implementation.extendsFrom(configurations.extraLibs)
}

jar {
    manifest {
        attributes 'Implementation-Title': rootProject.name,
                'Implementation-Version': version,
                'Main-Class': mainClassName
    }

    from {
        configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/sloud/database-utilities")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }

    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}
