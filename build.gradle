buildscript {
    repositories {
        jcenter()

    }
    dependencies {
        // Check for the latest version here: http://plugins.gradle.org/plugin/com.jfrog.artifactory
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4+"
    }
}

allprojects {
    apply plugin: "com.jfrog.artifactory"
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8
targetCompatibility = 1.8

def libraryGroupId = 'com.reynke.sloud'
def libraryArtifactId = 'databaseutilities'
def libraryVersion = '1.0.0'
def libraryMainClassName = 'SloudDatabaseUtilities'

group libraryGroupId + '.' + libraryArtifactId
version libraryVersion
mainClassName = group + '.' + libraryMainClassName

repositories {
    mavenCentral()
}

configurations {
    // Configuration that holds jars to include in the jar
    extraLibs
}

dependencies {
    compile group: 'com.google.inject', name: 'guice', version: '4.1.0'
    extraLibs group: 'mysql', name: 'mysql-connector-java', version: '6.0.6'
    extraLibs group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '2.1.2'
    extraLibs group: 'org.postgresql', name: 'postgresql', version: '42.1.4'
    extraLibs group: 'com.zaxxer', name: 'HikariCP', version: '2.7.2'
    extraLibs group: 'org.hibernate', name: 'hibernate-hikaricp', version: '5.2.12.Final'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    configurations.compile.extendsFrom(configurations.extraLibs)
}

jar {
    manifest {
        attributes 'Implementation-Title': rootProject.name,
                'Implementation-Version': version,
                'Main-Class': mainClassName
    }

    from {
        configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

artifactory {
    contextUrl = 'https://artifactory.reynke.com/artifactory'
    publish {
        repository {
            repoKey = version.endsWith('SNAPSHOT') ? 'gradle-dev-local' : 'gradle-release-local'
            username = reynke_artifactory_username
            password = reynke_artifactory_password
        }
        defaults {
            publications('mavenJava')
        }
    }
    resolve {
        repository {
            repoKey = 'gradle-release'
            username = reynke_artifactory_username
            password = reynke_artifactory_password
            maven = true
        }
    }
}

artifactoryPublish {
    dependsOn jar
}
